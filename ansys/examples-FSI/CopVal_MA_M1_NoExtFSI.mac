!/////////////////////////////////////////////////////////////////////////
! FE MODEL - MODAL ANALYSIS FOR FIV VALIDATION
! Authors: Lloyd Fletcher
! Units: N, m, Pa
!/////////////////////////////////////////////////////////////////////////

! These two commands clear out ANSYS and set the working directory
FINISH
/CLEAR,NOSTART 

!PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP
! Set path variables
useWorkPath = 1
*IF,useWorkPath,EQ,1,THEN
    PATH_A = 'D:\'
    PATH_B = ''
    PATH_C = 'FEA_ANSYS_Work_Dir\'
*ElSEIF,useWorkPath,EQ,2,THEN
    PATH_A = 'C:\'
    PATH_B = 'TempWorkingDirectory\'
    PATH_C = 'LloydFletcher\'
*ELSE
    PATH_A = 'D:\'
    PATH_B = 'UKAEA_Work\'
    PATH_C = 'FEA_ANSYS_Work_Dir\'
*ENDIF

! Core Paths
PATH_D = 'lf-fiv-val\'
PATH_E = 'models-noextfsi\'
! Function and Data Path
PATH_FUNC = 'funcs\'
PATH_DATA = 'data_ma_m1_cv\'
PATH_FLOAD = 'fluidload_m1_cv\'

! Set working directory to the function path first
/CWD,%PATH_A%%PATH_B%%PATH_C%%PATH_D%%PATH_E%%PATH_FUNC%
!PPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPPP

Jobname = 'MODAL_CV_NOEXTFSI'
/TITLE,Jobname
/FILNAME,Jobname
! Makes trig functions work in degrees instead of radians 
*AFUN,DEG   
pi = 3.141592653589793
/SHOW,CLOSE 
/SHOW, 

/NERR,,90000000

/PREP7 
!-------------------------------------------------------------------------
! PARAMETER DEFINITIONS
/PNUM,KP,0  
/PNUM,LINE,0
/PNUM,AREA,0

! Solve Flags
solveStatic = 0
solveModal = 1
incPStress = 0
numModes = 10
extFSI = 0

! Post-Processing Flags
postPlotMesh = 1
post1WriteFreqs = 1
post1PlotModes = 1

! Physics Flags - NOTE NO EXT FSI HERE!
useAcousticPressBC = 0

! Damping Parameters
dampOn = 0
dampRatio = 0.0 !0.5/100
dampFreq1 = 0.0		! Set this to zero - no mass damping
dampFreq2 = 41.59 	! Set this to the first natural freq
! f1=41.59Hz , f2=64.7Hz, f3 =125.2Hz

! Mesh size variables
meshRefFact = 1
pipeLengElems = NINT(50*meshRefFact)
pipeCircElems = NINT(12*meshRefFact)
! Mesh refinement variables
!refMeshFlag = 0
!refLocalSelDiams = 1
!refLocalFactor = 1
! 1 = 12->36 = x3
! 2 = 12->36 = x3
! 3 = 12->108 = x9
! 5 = 12->108 = x9

! Calculated Damping Parameters
dampOm1 = 2*PI*dampFreq1
dampOm2 = 2*PI*dampFreq2
*IF,dampOn,EQ,1,THEN
	dampAlpha = 2*dampRatio*(dampOm1*dampOm2)/(dampOm1+dampOm2)
	dampBeta = 2*dampRatio/(dampOm1+dampOm2)
*ELSE
	dampRatio = 0.0
	dampAlpha = 0.0
	dampBeta = 0.0
*ENDIF

!-------------------------------------------------------------------------
! Loads parameters

! Pipe Loads
pipeIntPressOn = 0	! NOTE: turned off in functions
pipeIntPress = 15e6 ! Pa
pipeExtPressOn = 0	! Applies 'bending' pressure on top of pipe
pipeExtPress = 8 ! equivalent to buoyancy force in Pa 
! NOTE: drag force calcs
! PbLi @ 1mm/s = 0.0057 Pa
! PbLi @ 10mm/s = 0.57 Pa                               
! PbLi @ 100mm/s = 57 Pa

!-------------------------------------------------------------------------
! GEOMETRY
F_DefGeom_CV_NoExtFSI

!-------------------------------------------------------------------------
! ELEMENTS AND MATERIALS
F_DefMatAndElems_CV_NoExtFSI

!-------------------------------------------------------------------------
! MESHING
F_Mesh_CV_NoExtFSI

!-------------------------------------------------------------------------
! BOUNDARY CONDITIONS, LOADs, OUTPUT NODES/ELEMS
F_DefBCs_CV_NoExtFSI
F_DefPStress_CV_NoExtFSI

/PSF,DEFA, ,1,0,1   
/PBF,DEFA, ,1
/PBC,ALL, ,1
/PSF,PRES,NORM,2,1,1
/VIEW,1,1,2,3   
/ANG,1  
EPLOT

!-------------------------------------------------------------------------
! SOLUTION - QUASI-STATIC (for pre-stress)
/SOLU
*IF,solveStatic,EQ,1,THEN
checkSolveStatic = 'on'
F_SolveQuasiStatic
*ELSE
checkSolveStatic = 'off'
*ENDIF
FINISH

!-------------------------------------------------------------------------
! SOLUTION - MODAL ANALYSIS
/SOLU
*IF,solveModal,EQ,1,THEN
checkSolveModal = 'on'
F_SolveModal
*ELSE
checkSolveModal = 'off'
*ENDIF
FINISH

!-------------------------------------------------------------------------
! POST-PROCESSING - General
/CWD,%PATH_A%%PATH_B%%PATH_C%%PATH_D%%PATH_E%%PATH_FUNC%
F_Modal_Post1

!-------------------------------------------------------------------------
! POST-PROCESSING - Plot first mode / deformation to check it worked
/POST1
/DSCALE,ALL,0.0   ! Displacement shown at 1:1 scale or AUTO/0
/GRAPHICS,FULL  ! Make sure all results are available for listing/plotting

/CWD,%PATH_A%%PATH_B%%PATH_C%%PATH_D%%PATH_E%%PATH_DATA%
! For pre-stress analysis read in modal results
*IF,incPStress,EQ,1,THEN
*IF,solveModal,EQ,1,THEN
FILE,,rstp ! Read in the modal analysis results
*ENDIF
*ENDIF

ALLSEL
ESEL,S,TYPE,,1
NSLE,S
SET,1,1
PLNSOL, U,SUM,0,1.0


